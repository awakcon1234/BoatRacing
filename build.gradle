plugins {
    id 'java'
    // Switch to new Shadow plugin coordinates compatible with Gradle 9
    id 'com.gradleup.shadow' version '9.2.1'
}

group = 'dev.belikhun'
version = '1.0.9'

description = 'BoatRacing Bukkit/Spigot plugin'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
    withSourcesJar()
}

repositories {
    mavenCentral()
    maven { url = uri('https://repo.papermc.io/repository/maven-public/') }
    maven { url = uri('https://repo.codemc.io/repository/maven-snapshots/') }
    maven { url = uri('https://repo.codemc.org/repository/maven-public/') }
    maven { url = uri('https://repo.extendedclip.com/content/repositories/placeholderapi/') }
    // MapEngine (map-based GUI rendering)
    maven { url = uri('https://repo.minceraft.dev/releases/') }
}

dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.21-R0.1-SNAPSHOT'

    // MapEngine API (runtime provided by MapEngine plugin)
    compileOnly 'de.pianoman911:mapengine-api:1.8.11'

    implementation 'net.wesjd:anvilgui:1.10.8-SNAPSHOT'
    // Megavex scoreboard-library
    implementation 'net.megavex:scoreboard-library-api:2.4.4'
    runtimeOnly 'net.megavex:scoreboard-library-implementation:2.4.4'
    runtimeOnly 'net.megavex:scoreboard-library-modern:2.4.4'
    // PlaceholderAPI (optional at runtime)
    compileOnly 'me.clip:placeholderapi:2.11.7'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.0'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.10.1'
    testImplementation 'io.papermc.paper:paper-api:1.21-R0.1-SNAPSHOT'
}

test {
    useJUnitPlatform()
}

tasks.withType(JavaCompile).configureEach {
    options.release = 21
    options.encoding = 'UTF-8'
    // Show detailed deprecation warnings during compilation
    options.compilerArgs += ['-Xlint:deprecation']
}

processResources {
    // Use a Provider for the version to avoid accessing Task.project at execution time
    def versionProvider = project.provider { project.version }
    filesMatching('plugin.yml') {
        expand([version: versionProvider])
    }
}

import org.gradle.api.file.DuplicatesStrategy

tasks.withType(Jar).configureEach {
    archiveBaseName.set('BoatRacing')
    archiveVersion.set('')
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

shadowJar {
    archiveBaseName.set('BoatRacing')
    archiveVersion.set('')
}

tasks.build { dependsOn tasks.shadowJar }
